plugins {
    id("com.diffplug.spotless") version "6.11.0" apply false
}

allprojects {
    apply(plugin: "java")
    apply(plugin: "com.diffplug.spotless")

    repositories {
        mavenCentral()
    }

    spotless {
        java {
            target("src/*/java/**/*.java")

            endWithNewline()
            indentWithSpaces()
            removeUnusedImports()
            toggleOffOn()
            eclipse().configFile(rootProject.file("codeformat/codeformat.xml"))
        }
    }
}

subprojects {
    if (projectDir.parentFile != rootProject.file("tests")) {
        return
    }

    apply(plugin: "application")

    dependencies {
        implementation(project(":"))
    }

    task runRvm(type: Exec) {
        dependsOn(project(":rvm-classpath").jar)
        dependsOn(jar)

        workingDir(rootProject.file(".."))
        commandLine("cargo", "+nightly", "run", project(":rvm-classpath").jar.archivePath, jar.archivePath)
    }

    application {
        mainClass.set("Main")
    }
}
